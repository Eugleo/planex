plugins {
    id "application"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.apache.poi:poi:4.1.2") // xls reading
    implementation("org.apache.poi:poi-ooxml:4.1.2") // xlsx reading
    implementation("org.jsoup:jsoup:1.13.1") // html parsing
    implementation(files("lib/local/com.google.ortools.jar")) // cp solver
    implementation("com.google.protobuf:protobuf-java:3.11.4") // ortools dependency
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
}

application {
    mainClassName = "com.wybitul.planex.Main"
    applicationDefaultJvmArgs = ["-Djava.library.path=lib/native"]
    version = "1.0.2"

    run {
        standardInput = System.in
    }
}

distributions {
    main {
        contents {
            from "README.md"
            from "config-doc.md"
            from("lib") {
                into "lib"
            }
        }
    }
}

startScripts {
    defaultJvmOpts = ["-Djava.library.path=APP_HOME_DIR/lib/native"]
    doLast {
        unixScript.text = unixScript.text.replace('APP_HOME_DIR', '\$APP_HOME')
        windowsScript.text = windowsScript.text.replace('APP_HOME_DIR', '%APP_HOME%')
    }
}